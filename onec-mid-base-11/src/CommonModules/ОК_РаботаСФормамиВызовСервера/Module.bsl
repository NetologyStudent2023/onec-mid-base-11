#Область СлужебныйПрограммныйИнтерфейс

// {{ ОК начало доработки - решение задачи 1
Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
		ДобавитьКомандуКнопкуРассчитатьСоСкидкой(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  		
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);
	КонецЕсли;

КонецПроцедуры 


Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма) Экспорт  
	
	ПолеВвода = Форма.Элементы.Добавить("ОК_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Заголовок = "Контактное лицо (добавленный объект)";
	ПолеВвода.ПутьКДанным = "Объект.ОК_КонтактноеЛицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
    	
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма) Экспорт  
	
	ПолеВвода = Форма.Элементы.Вставить("ОК_КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Заголовок = "Контактное лицо (добавленный объект)";
	ПолеВвода.ПутьКДанным = "Объект.ОК_КонтактноеЛицо";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
    	
КонецПроцедуры 
   // конец доработки }}

   // {{ ОК  начало доработки - решение задачи 2
Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма) Экспорт 
	
	Группа = Форма.Элементы.Добавить("ОК_ГруппаЭлементов", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	ПолеВвода = Форма.Элементы.Добавить("ОК_СогласованнаяСкидка", Тип("ПолеФормы"), Группа);
	ПолеВвода.Заголовок = "Согласованная скидка (добавленный объект)";
	ПолеВвода.ПутьКДанным = "Объект.ОК_СогласованнаяСкидка";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.УстановитьДействие("ПриИзменении", "ОК_СогласованнаяСкидкаПриИзменении");
    	
КонецПроцедуры 


Процедура ДобавитьКомандуКнопкуРассчитатьСоСкидкой(Форма) Экспорт  
	
	Команда = Форма.Команды.Добавить("ОК_РассчитатьСоСкидкой");
	Команда.Действие = "ОК_КомандаРассчитатьСоСкидкой"; 
	
	Кнопка = Форма.Элементы.Добавить("ОК_КомандаРассчитатьСоСкидкой", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = "ОК_РассчитатьСоСкидкой"; 
	Кнопка.Заголовок = "Рассчитать со скидкой (добавленный объект)";
    	
КонецПроцедуры 

// конец доработки }} 

#КонецОбласти